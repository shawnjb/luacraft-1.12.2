buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.anatawa12.com/maven" }
        mavenCentral()
    }
    dependencies {
        classpath 'com.anatawa12.forge:ForgeGradle:2.3-1.0.+'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.0"
group = "com.shawnjb.luacraft"
archivesBaseName = "luacraft"

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

minecraft {
    version = project.forge_version
    runDir = "run"
    mappings = project.mcp_version
}

dependencies {
    compile files('./jars/luaj-jse-3.0.2.jar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task generateLuaDocs(type: JavaExec) {
    group = 'documentation'
    description = 'Generates Lua API documentation for LuaCraft'
    classpath = files("$buildDir/classes/java/main", sourceSets.main.compileClasspath)
    main = 'com.shawnjb.luacraft.docs.LuaDocGenerator'

    doFirst {
        println '[LuaCraft] Generating LuaCATS documentation (docs.lua)...'
        file("src/main/resources/docs").mkdirs()
    }

    outputs.upToDateWhen { false }
}

generateLuaDocs.dependsOn classes

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
